# Maintainer: Roberto Catini <roberto.catini@gmail.com>

pkgname=calld
pkgrel=1
pkgver=0
pkgdesc="Call peer-to-peer network daemon"
arch=('i686' 'x86_64')
url="https://github.com/callchain/calld"
license=('custom:ISC')
depends=('protobuf' 'openssl' 'boost-libs')
makedepends=('git' 'scons' 'boost')
backup=("etc/$pkgname/calld.cfg")
source=("git://github.com/callchain/calld.git#branch=master")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	scons
}

check() {
	cd "$srcdir/$pkgname"
	build/calld --unittest
}

package() {
	cd "$srcdir/$pkgname"
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D build/calld "$pkgdir/usr/bin/calld"
	install -D -m644 doc/calld-example.cfg "$pkgdir/etc/$pkgname/calld.cfg"
	mkdir -p "$pkgdir/var/lib/$pkgname/db"
	mkdir -p "$pkgdir/var/log/$pkgname"
}
